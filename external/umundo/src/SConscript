import os
Import('env')
local_env = env.Clone()

isShared = False

cppflags = [
'-Wall',
'-Wno-attributes',
'-Wno-uninitialized',
'-Wno-narrowing',
'-Wno-unused-local-typedefs',
'-fno-strict-aliasing',
'-Wno-unused-value',
'-Wno-unused-function',
'-Wno-sign-compare']

if GetOption('release'):
    cppflags.append('-DBUILD_RELEASE')

if isShared:
    cppflags.extend(['-DCOMPILING_DLL', '-Dumundocore_EXPORTS'])
else:
    cppflags.extend(['-DUMUNDO_STATIC', '-DZMQ_STATIC'])

pathBoost   = os.environ["BOOST_DIR"];
cppflags.extend(['-isystem', pathBoost  + '/include']);

cxxpaths=['#/external/umundo/src',
          '#/external/umundo/fastlz',
          '#/external/umundo/zeromq-4.1.0/include',
          '#/external/umundo/mDNSResponder-333.10/mDNSCore',
          '#/external/umundo/mDNSResponder-333.10/mDNSShared',
          '#/external/umundo/re-0.4.7/include']

if GetOption('win64'):
    cxxpaths.append('#/external/umundo/src/include_win');
    cxxpaths.append('#/external/umundo/mDNSResponder-333.10/mDNSWindows');
else:
    cxxpaths.append('#/external/umundo/src/include_linux');
    cxxpaths.append('#/external/umundo/mDNSResponder-333.10/mDNSPosix');


local_env.Append(CPPPATH = cxxpaths)
local_env.Append(CPPFLAGS = cppflags)


coreFiles = ['Message.cpp',
             'Debug.cpp',
             'Error.cpp',
             'UUID.cpp',
             'Host.cpp',
             'Factory.cpp',
             'portability.cpp',
             'connection/Publisher.cpp',
             'connection/Node.cpp',
             'connection/Subscriber.cpp',
             'discovery/Discovery.cpp',
             'thread/tinythread.cpp',
             'thread/Thread.cpp',
             'connection/zeromq/ZeroMQSubscriber.cpp',
             'connection/zeromq/ZeroMQNode.cpp',
             'connection/zeromq/ZeroMQPublisher.cpp',
             'connection/rtp/RTPPublisher.cpp',
             'connection/rtp/RTPThread.cpp',
             'connection/rtp/RTPSubscriber.cpp',
             'discovery/MDNSDiscovery.cpp',
             'discovery/mdns/bonjour/BonjourDiscoverer.cpp',
             'discovery/BroadcastDiscovery.cpp',
             'discovery/mdns/bonjour/mDNSEmbedded.c'
]

fastlzFiles = '#/external/umundo/fastlz/fastlz.c'

objs = []
if isShared:
    for f in coreFiles:
        objs.append(local_env.SharedObject('umundo/core/' + f))
    objs.append(local_env.SharedObject(fastlzFiles))
    coreLib = local_env.SharedLibrary(env['base_lib_dir'] + '/libumundocore64', objs)
else:
    for f in coreFiles:
        objs.append(local_env.StaticObject('umundo/core/' + f))
    objs.append(local_env.StaticObject(fastlzFiles))
    coreLib = local_env.StaticLibrary(env['base_lib_dir'] + '/libumundocore64', objs)

Default(coreLib)
